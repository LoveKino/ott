# be careful: space between symbols

PROGRAM := EXPRESSION    @id($1)

EXPRESSION := FUN_EXPRESSION    @id($1)
    | DATA    @id($1)
    | XML_EXPRESSION
    | variableName              @id(lookupVariable($1))

# XML class
XML_EXPRESSION := < variableName XML_ATTRS />
     | < variableName XML_ATTRS > XML_EXPRESSION_LIST </ variableName >

XML_EXPRESSION_LIST := EPSILON
     | XML_EXPRESSION
     | XML_EXPRESSION XML_EXPRESSION_LIST

XML_ATTRS := EPSILON
     |  XML_ATTR
     |  XML_ATTR XML_ATTRS
XML_ATTR := variableName assign EXPRESSION

# fun-style
FUN_EXPRESSION := FUN_NAME ( EXP_COMMA_LIST )    @applyFun($1, $3)
FUN_NAME := variableName        @lookupVariable($1)
     | FUN_EXPRESSION           @id($1)
     | ( FUN_EXPRESSION )       @id($2)

# JSON-style data
DATA := string    @string($1)
     |  number    @number($1)
     |  null      @null
     |  true      @true
     |  false     @false
     |  ARRAY     @id($1)
     |  OBJECT    @id($1)

ARRAY := [ EXP_COMMA_LIST ]    @id($2)

OBJECT := { EXP_COLON_PAIR_LIST }    @object($2)

# expression list
EXP_COMMA_LIST :=  EPSILON                   @array()
         |  EXPRESSION                       @array($1)
         |  EXPRESSION , EXP_COMMA_LIST      @appendTo($1, $3)

EXP_COLON_PAIR_LIST :=  EPSILON                     @array()
         |  EXP_COLON_PAIR                          @array($1)
         |  EXP_COLON_PAIR , EXP_COLON_PAIR_LIST    @appendTo($1, $3)

EXP_COLON_PAIR := EXP_PAIR_KEY colon EXPRESSION     @array($1, $3)

EXP_PAIR_KEY := string      @string($1)
         |  variableName    @id($1)
